.program spdif_subframe_decode

; Assumes line is idle low
; One bit is ~40 cycles. In each bit period:
; - A '0' is encoded as a transition at time 0
; - A '1' is encoded as a transition at time 0 and a transition at time T/2 (10 cycles)
;
; The IN mapping and the JMP pin select must both be mapped to the GPIO used for
; RX data. Autopush must be enabled.

public start:
    set y, 28
preamble_start:
    jmp pin preamble_low

.wrap_target
preamble_high: ; 1110xxx0
    wait 1 pin 0            [14] ; 1, there is no biphase mark for 4 bits
    jmp pin p_high_1        [9]  ; 1, must stay at 1
    jmp preamble_low             ; restart with preamble low
p_high_1:
    jmp pin p_high_2        [9]  ; 1, must stay at 1
    jmp preamble_low             ; restart with preamble low
p_high_2:
    jmp pin preamble_low    [9]  ; must be 0, 3 previous was 1
preamble_end:
    in pins, 1              [9]  ; a
    in pins, 1              [9]  ; b
    in pins, 1              [8]  ; c
    in pins, 1              [0]  ; must be 1 for 1110, 0 for 0001
    jmp pin initial_low          ; must be 0
    jmp initial_high

preamble_low: ; 0001xxx1
    wait 0 pin 0            [14] ; 0, there is no biphase mark for 4 bits
    jmp pin preamble_high   [9]  ; 0, must stay at 0
    jmp pin preamble_high   [8]  ; 0, must stay at 0
p_low_2:
    jmp pin preamble_end          ; must be 1, 3 previous was 0
.wrap

initial_high:                    ; Find rising edge at start of bit period
    jmp y-- start
    wait 1 pin, 0           [13] ; Delay to eye of second half-period (i.e 3/4 of way
    jmp pin high_0               ; through bit) and branch on RX pin high/low.
high_1:
    in x, 1                      ; Second transition detected (a `1` data symbol)
    jmp initial_high
high_0:
    in null, 1                   ; Line still high, no centre transition (data is `0`)
    ; Fall-through

initial_low:                     ; Find falling edge at start of bit period
    jmp y-- start
    wait 0 pin, 0           [13] ; Delay to eye of second half-period
    jmp pin low_1
low_0:
    in null, 1                   ; Line still low, no centre transition (data is `0`)
    jmp initial_high
low_1:                           ; Second transition detected (data is `1`)
    in x, 1
    jmp initial_low

% c-sdk {
static inline void spdif_subframe_decode_program_init(PIO pio, uint sm, uint offset, uint pin, float div) {
    pio_sm_set_consecutive_pindirs(pio, sm, pin, 1, false);
    pio_gpio_init(pio, pin);

    pio_sm_config c = spdif_subframe_decode_program_get_default_config(offset);
    sm_config_set_in_pins(&c, pin); // for WAIT
    sm_config_set_jmp_pin(&c, pin); // for JMP
    sm_config_set_in_shift(&c, !true, true, 32); // LSB
    sm_config_set_fifo_join(&c, PIO_FIFO_JOIN_RX);
    sm_config_set_clkdiv(&c, div);
    pio_sm_init(pio, sm, offset, &c);

    // X is set to 1, to conveniently emit it to ISR/FIFO.
    pio_sm_exec(pio, sm, pio_encode_set(pio_x, 0xFFFFFFFF));
    pio_sm_set_enabled(pio, sm, true);
}
%}